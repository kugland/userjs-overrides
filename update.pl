#!/usr/bin/env -S perl -w

use 5.012;
use strict;
use warnings;
use autodie;

use File::Spec::Functions qw{ catfile };
use Env qw{ $FIREFOX_HOME $HOME $FIREFOX_PROFILE };
use File::Fetch;


my $OVERRIDES = do {
  no autodie;
  local $/;
  open my $fh, '<', 'user-overrides.js';
  <$fh>;
} // die "Could not read user-overrides.js: $!";


sub get_default_profile {
  my $path    = undef;
  my $default = undef;

  $FIREFOX_HOME //= "$HOME/.mozilla/firefox";

  open my $fh, '<', "$FIREFOX_HOME/profiles.ini";

  while (<$fh>) {
    chomp;

    if (m/ \A \[ .* \] \z /msx) {
      if ( $default && $path ) {
        say "Found default profile: $FIREFOX_HOME/$path";
        return catfile( $FIREFOX_HOME, $path );
      }
      $path    = undef;
      $default = undef;
    } ## end if (m/ \A \[ .* \] \z /msx)
    elsif (m/ \A Path= (.*) \z /msx) {
      $path = $1;
    }
    elsif (m/ \A Default=1 \z /msx) {
      $default = 1;
    }
  } ## end while (<$fh>)

  close $fh;

  die "Could not find default profile";
} ## end sub get_default_profile


sub get_user_js {
  print "Fetching user.js from GitHub... ";
  my $user_js = undef;
  my $ff = File::Fetch->new( uri => 'https://raw.githubusercontent.com/arkenfox/user.js/master/user.js' );
  $ff->fetch( to => \$user_js ) or die $ff->error;
  say "ok";
  return $user_js;
} ## end sub get_user_js

my $user_js_path = catfile( get_default_profile(), 'user.js' );

my $user_js = get_user_js();

do {
  print "Writing $user_js_path... ";
  open my $fh, '>', $user_js_path;

  print {$fh} "// *** THIS FILE IS AUTOMATICALLY GENERATED, DO NOT EDIT ***\n\n";
  print {$fh} $user_js;
  print {$fh} "\n\n// *** OVERRIDES START HERE ***\n\n";
  print {$fh} $OVERRIDES;
  print {$fh} "\n";

  close $fh;
  say "ok";
};

say "Please restart Firefox to apply changes";
